// Copyright (c) 2011, David J. Pearce (djp@ecs.vuw.ac.nz)
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//    * Redistributions of source code must retain the above copyright
//      notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//      notice, this list of conditions and the following disclaimer in the
//      documentation and/or other materials provided with the distribution.
//    * Neither the name of the <organization> nor the
//      names of its contributors may be used to endorse or promote products
//      derived from this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
// WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL DAVID J. PEARCE BE LIABLE FOR ANY
// DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
// (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
// LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
// ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
// SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

include "Core.wyrl"

// ====================================================================
// Multplication
// ====================================================================

// A multiplication term consists of a constant, and zero or more 
// variable multipliers.  For example, 2*x*y.
term Mul[real,{|AExpr...|}]

// Zero multiplied by anything gives zero (1); A term with no
// multipliers is just a number (2).
reduce Mul[real n, {|*... rest|}]:
    => Num(n), if n == 0.0 || |rest| == 0

// Simplify terms containing constant multipliers.
reduce Mul[real x, {|Num(real y), *... rest|}]:
    => Mul[x*y,rest]

// Flattern nested multiplication terms.
reduce Mul[real n1, {|Mul[real n2,{|*... xs|}], *... ys|}]:
    => Mul[n1*n2,(xs ++ ys)]

// Distribute multiplication through addition.
reduce Mul[real n1, {|Sum[real n2, {|*... xs|}], *... ys|}]:
    => let ys = {| Mul[n1, (x ++ ys)] | x in xs |}
       in Sum[n1*n2,ys]

// ====================================================================
// Division
// ====================================================================

term Div[AExpr,AExpr]

// TO DO!

// ====================================================================
// Summation
// ====================================================================

// A summation consists of a constant, and zero or more 
// multiplication components.  For example, 2+(1*x)+(1*y) 
// and also 2+(x*y)+z.
term Sum[real,{|AExpr...|}]

// A summation with no components is just a number.
reduce Sum[real n, {||}]:
    => Num(n)

// Normalise so that all variable summation components are multiplications.
reduce Sum[real n, {|AExpr x, *... rest|}]:
    => Sum [n,Mul[1.0,{|x|}] ++ rest], if !(x is Num || x is Sum || x is Mul)

// Simplify summations with constant components.
reduce Sum[real x, {|Num(real y), *... rest|}]:
    => Sum[x+y,rest]

// Simplify summations with related multication components.  
// For example, (2*x*y)+(3*x*y)+(2*x) ==> (5*x*y) + (2*x)
reduce Sum[real n, {|Mul[real x, {|*...|} xs], Mul[real y, {|*...|} ys], *... zs|}]:
    => Sum[n, Mul[x+y, xs] ++ zs], if xs == ys

// Flattern summations.
reduce Sum[real x, {|Sum[real y, {|*... ys|}], *... xs|}]:
    => Sum[x+y, xs ++ ys]

// ====================================================================
// Equations
// ====================================================================

// The "arithmetic types", which are used for equations and inequalities
define AType as IntT | RealT 

// An equation should be viewed as equating a given expression with
// zero.  
term Equation[AType,AExpr]

reduce Equation[AType,Num(real v)]:
    => False, if v != 0.0
    => True

// TODO: substitute least VExpr in Equation.

// TEMPORARY HACK (should be replaced by vexpr substitution above)
infer And{Equation[AType t1, Sum[real x1, {|Mul[real x2, {|AExpr v1|}], Mul... xs|}]] eq1,
      Inequality[AType t2, Sum[real y1, {|Mul[real y2, {|AExpr v2, *... vs|}], Mul... ys|}]] eq2,
      BExpr... rest}:
     => let eq3 = eq2[v1 \ Mul[-1.0 / x2,{|Sum[x1,xs]|}]]
        in And ({eq1,eq2,eq3} ++ rest), if v1 == v2 && t1 == t2

// 0 != e => 0 <= e-1 || 0 <= -e-1 (i.e. 1+e <= 0)
reduce Not(Equation[IntT t,AExpr e]):
    => let neg_em1 = Sum[-1.0,{|Mul[-1.0,{|e|}]|}],
           em1 = Sum[-1.0,{|e|}]
       in Or{Inequality[t,neg_em1],Inequality[t,em1]}

// 0 != e => 0 < e || 0 < -e (i.e. e < 0)
reduce Not(Equation[RealT t,AExpr e]):
    => let neg_e = Mul[-1.0,{|e|}]
       in Or{Inequality[t,neg_e],Inequality[t,e]}

reduce Equals[AType t,{|AExpr e1,AExpr e2|}]:
    => let neg_e1 = Mul[-1.0,{|e1|}]
       in Equation[t,Sum[0.0,{|neg_e1,e2|}]]

// ====================================================================
// Inqualities
// ====================================================================

// Integer inequalities are non-strict (i.e. of the form 0 <= e) to avoid
// an unnecessary disjunction.  That is, if we implement them as strict
// inequalities (i.e. of the form 0 < x), then 0 <= x becomes 0 < x || x == 0.
//
// Rational Inequalities are strict inequalities, unlike integer
// inequalities which are non-strict.  Again, this is to avoid an
// unnecessary (and complex in this case) disjunction.  That is, if real
// inequalities we non-strict then 0 < e would become 0 <= e && e != 0,
// which is actually hard to implement.
term Inequality[AType,AExpr]

// Handle comparison against constant values, which always reduce to
// either true or false.  Care must be taken int the case of a constant
// which equals zero, since integer inequalities are non-strict whilst 
// real inequalities are strict.
reduce Inequality[AType t, Num(real v)]:
    => False, if v < 0.0
    => False, if v == 0.0 && t == RealT    
    => True

// Handle case of an negated integer inequality.  In such case, we can
// safely encode this as another integer inequality, although care must
// be taken as this only works for integer inequalities (which are
// non-strict).  As an example !(x <= 1) gives x >= 2.  In the general
// case, we have !(0 <= e) ==> 0 <= -e - 1 (i.e. e <= -1)
reduce Not(Inequality[IntT t, AExpr e]):
    => let neg_e = Mul[-1.0,{|e|}]
       in Inequality[t, Sum[-1.0,{|neg_e|}]]

// Handle case where we have x <= y <= x (for integer inequalities).
// In this case, we can infer that x == y.  Case must be taken as this
// only works for integer inequalities (which are non-strict).
//
// FIXME: this rule could be generalised further, however it's tricky
// to do this correctly because we need a way to check that all the
// multiplicands in ieq2 and the negation of those in ieq1.
//
// FIXME: this rule currently kills performance and may be causing
// some kind of loop.  We need this rule, but it doesn't work properly
// yet (for unknown reasons).
//
// reduce And{Inequality[IntT, Sum[real x1, {|Mul[real x2,{|AExpr... v1s|}] xs|}] s1] ieq1,
//           Inequality[IntT, Sum[real y1, {|Mul[real y2,{|AExpr... v2s|}] ys|}] s2] ieq2, BExpr... rest}:
//     => And (Equals[IntT,{|s1,Num(0.0)|}] ++ rest), if x1 == -y1 && x2 == -y2 && v1s == v2s

// TODO: real negated inequalities

// Handle closure over inequalities.  For example, if x <= y and y <=
// z then we want to infer that x <= z.  This must be an inference
// rule since it potentially increases the number of clauses (and, hence,
// would potentially cause infinite loops as a reduction).
infer And{Inequality[AType t1, Sum[real x1, {|Mul[real x2, {|AExpr v1|}], *... xs|}] s1] eq1, 
          Inequality[AType t2, Sum[real y1, {|Mul[real y2, {|AExpr v2|}], *... ys|}] s2] eq2, 
	  BExpr... rest}:
     => let s3 = Mul[y2,{|Sum[x1,xs]|}],
            s4 = Mul[-x2,{|Sum[y1,ys]|}],
            eq3 = Inequality[t1,Sum[0.0,{|s3,s4|}]]
        in And ({eq1,eq2,eq3} ++ rest), if v1 == v2 && t1 == t2 && x2 < 0.0 && y2 > 0.0

// ====================================================================
// Arithmetic Expressions
// ====================================================================

define BExpr as Inequality | Equation | ...

define AExpr as Num | Sum | Mul | Div | VExpr

define Expr as AExpr | ...