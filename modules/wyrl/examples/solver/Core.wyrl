// Copyright (c) 2011, David J. Pearce (djp@ecs.vuw.ac.nz)
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//    * Redistributions of source code must retain the above copyright
//      notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//      notice, this list of conditions and the following disclaimer in the
//      documentation and/or other materials provided with the distribution.
//    * Neither the name of the <organization> nor the
//      names of its contributors may be used to endorse or promote products
//      derived from this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
// WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL DAVID J. PEARCE BE LIABLE FOR ANY
// DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
// (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
// LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
// ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
// SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

// ====================================================================
// Bool
// ====================================================================

term True
term False
define Bool as True | False

// ====================================================================
// Variables
// ====================================================================

// A variable is an expression identified solely by a string whose
// value is currently unknown.
term Var(string)

// ====================================================================
// Tuples
// ====================================================================

// A tuple represents a sequence of zero or more expressions.  In
// otherwords, it's a compound expression built up from other
// expressions.  For example, the pair (1,2) is a tuple consisting of two
// expressions, namely 1 and 2.
term Tuple[Expr...]

// A tuple load operation selects the expression at a given (fixed)
// index within a tuple.
term Load[Expr,int]

// Select the expression at the given index within a tuple.  This
// requires that the index is within the bounds of the tuple.
reduce Load[Tuple[Expr... ls], int idx]:
    => ls[idx], if idx >= 0 && idx < |ls|

// Two tuples of different sizes are never equal (1); two tuples of
// the same size are equal iff subexpressions at matching indices are
// equal.
reduce Equals[Tuple[Expr... xs],Tuple[Expr... ys]]:
    => False, if |xs| != |ys|
    => let eqs = { Equals[xs[i],ys[i]] | i in 0..|xs| }
       in And(eqs)

// ====================================================================
// Uninterpreted Functions
// ====================================================================

// An uninterpreted function is an expression representing an external
// function call.  The only interpretation provided is that these are
// mathematical functions which always return the same result given
// the same input.  For example, suppose an uninterpreted function f
// which takes two arguments.  Then, f(x1,y1) == f(x2,y2) if x1 == x2
// and y1 == y2.
term Fn[string,Expr...]

// ====================================================================
// Not
// ====================================================================

term Not(BExpr)

// Invert a boolean value.
reduce Not(Bool b):
    => False, if b == True
    => True

// Inverting an inverted expression yields the original expression.
reduce Not(Not(* x)):
    => x

// Apply De Morgan's law for inverting logical And.
reduce Not(And{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in Or(ys)

// Apply De Morgan's law for inverting logical Or.
reduce Not(Or{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in And(ys)

// ====================================================================
// And
// ====================================================================

term And{BExpr...}

reduce And{BExpr x}:
    => x

reduce And{Bool b, BExpr... xs}:
    => False, if b == False
    => True, if |xs| == 0
    => And (xs)

reduce And{And{BExpr... xs}, BExpr... ys}:
    => And (xs ++ ys)

reduce And{Not(BExpr x), BExpr y, BExpr... ys}:
    => False, if x == y

// doesn't need to be an infer, but this rule can be very expensive.
infer And{Or{BExpr... xs}, BExpr... ys}:
    => let ys = { And(x ++ ys) | x in xs }
       in Or(ys)

// ====================================================================
// Or
// ====================================================================

term Or{BExpr...}

reduce Or{BExpr x}:
    => x

reduce Or{Bool b, BExpr... xs}:
    => True, if b == True
    => False, if |xs| == 0
    => Or (xs)

reduce Or{Not(BExpr x), BExpr y, BExpr... ys}:
    => True, if x == y

reduce Or{Or{BExpr... xs}, BExpr... ys}:
    => Or (xs ++ ys)

// ====================================================================
// Equality
// ====================================================================

// FIXME: update equals to be a bag by supporting a global ordering of
// terms.
term Equals[Expr,Expr]

// An expression is always equal to itself (1); any two distinct
// values are never equal (2).  Notice that we cannot conclude False
// when we have general expressions that aren't equal, because these may 
// eventually reduce to the same value (or not).
reduce Equals[Expr x, Expr y]:
    => True, if x == y
    => False, if x != y && x is Value && y is Value

// Substitute any variable which is known to equal a value through
// for that value in all expressions.
reduce And{Equals[VExpr x, Value y] eq, BExpr... bs}:
    => let cs = { b[x\y] | b in bs }
       in And (eq ++ cs)

// Substitute any variable which is known to equal a value through
// for that value in all expressions.
reduce And{Equals[Value x, VExpr y] eq, BExpr... bs}:
    => let cs = { b[y\x] | b in bs }
       in And (eq ++ cs)

// When two variables are equal, substitute one for the other in all
// expressions.  We need to carefully choose a unique representative 
// from the two variables in order to prevent a loop where we substitute 
// for one variable, and then repeat this for the other, and so on.
reduce And{Equals[VExpr x, VExpr y] eq, BExpr... bs}:
    => let cs = { b[x\y] | b in bs }
       in And (eq ++ cs)

// ====================================================================
// Expressions & Values
// ====================================================================

// A value is a concrete term which cannot be further reduced.  The 
// base values are boolean true or false, and a tuple of values.  Other 
// values include integers, rationals and sets of values.
define Value as Bool | Tuple[Value...] | ... 

// Variable Expressions are those which represent variables or
// sub-components thereof.  Such expressions can in principle 
// return a value of any kind.  
define VExpr as Var | Load | ...

// Boolean expressions are those which can return a boolean value.  
// Variable expressions must be included in this, since they can return
// any kind of value.
define BExpr as VExpr | Bool | And | Or | Not | Equals | ...

// General expressions (i.e. *everything*).
define Expr as Value | Tuple | Fn | BExpr | ...








